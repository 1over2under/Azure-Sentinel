id: b5237116-c40a-4cbe-bac6-c35564a0c578
Function:
  Title: Dragos Notifications Sentinel Incidents
  Version: '1.0.0'
  LastUpdated: '2024-08-25'
Category: Microsoft Sentinel Parser
FunctionName: DragosNotificationsToSentinel
FunctionAlias: DragosNotificationsToSentinel
FunctionQuery: |
  // Define helper to map dragos severity to sentinel severity
  let DragosSeverityToSentinelSeverity = (DragosSeverity:int = 0) { case(DragosSeverity == 0, "Informational",
      DragosSeverity == 1, "Informational",
      DragosSeverity == 2, "Low",
      DragosSeverity == 3, "Medium",
      DragosSeverity == 4, "High",
      DragosSeverity == 5, "High",
      "Unknown")};
  //
  // Look back over existing incidents to prevent dupes
  let existingIncidents = SecurityAlert
  | where ProductName == "Dragos"
  | extend CustomDetails=tostring(parse_json(ExtendedProperties)["Custom Details"])
  | extend id = toint(extract_json("$.DragosNotificationId[0]", CustomDetails))
  | project-keep SystemAlertId, id;
  //
  // Grab notifications
  union isfuzzy=true DragosPushNotificationsToSentinel, DragosPullNotificationsToSentinel
  | join kind=leftouter (existingIncidents) on id
  | where isempty(SystemAlertId)
  | extend MSSentinelSeverity = DragosSeverityToSentinelSeverity(severity)
  | sort by severity desc
