id: 88632fa3-ba56-49bb-9b45-cd2ac79b99bd
Contentkind: Function
Description: GetPowerAppsEventDetails
Function:
  Title: GetPowerAppsEventDetails
  Version: 3.2.0
  LastUpdated: '2024-11-13'
Category: MSBizAppsFunctions
FunctionName: GetPowerAppsEventDetails
FunctionAlias: GetPowerAppsEventDetails
FunctionQuery: |
  let extract_value = (FieldName: string, PropertyCollection: string) {
      extract(strcat('"Name":"', FieldName, '","Value":"([^"]+)"'), 1, PropertyCollection)
  };
  T
  | extend PropertyCollection = tostring(PropertyCollection)
  | project
      TimeGenerated = column_ifexists('TimeGenerated', datetime(null)),
      EventOriginalUid = column_ifexists('EventOriginalUid', ''),
      EventOriginalType = extract_value("powerplatform.analytics.activity.name", PropertyCollection),
      ActorName = extract_value("enduser.principal_name", PropertyCollection),
      ActorUserId = extract_value("enduser.id", PropertyCollection),
      ActorUserType = extract_value("enduser.role", PropertyCollection),
      SrcIpAddr = extract_value("enduser.ip_address", PropertyCollection),
      TenantId = extract_value("powerplatform.analytics.resource.tenant.id", PropertyCollection),
      ResourceType = extract_value("powerplatform.analytics.resource.type", PropertyCollection),
      IsSuccess = extract_value("powerplatform.analytics.operation.is_successful", PropertyCollection),
      EnvironmentName = tolower(extract_value("powerplatform.analytics.resource.environment.name", PropertyCollection)),
      EnvironmentId = tolower(extract_value("powerplatform.analytics.resource.environment.id", PropertyCollection)),
      CorrelationId = extract_value("powerplatform.analytics.resource.telemetry_correlation.id", PropertyCollection),
      AppName = extract_value("powerplatform.analytics.resource.power_app.display_name", PropertyCollection),
      AppId = extract_value("powerplatform.analytics.resource.power_app.id", PropertyCollection),
      UserAgent = extract_value("user_agent.original", PropertyCollection),
      ConnectionId = extract_value("powerplatform.analytics.resource.connection.id", PropertyCollection),
      ConnectionName = extract_value("powerplatform.analytics.resource.connection.action_name", PropertyCollection),
      PermissionType = extract_value("powerplatform.analytics.resource.permission_type", PropertyCollection),
      TargetPrincipalId = extract_value("targetuser.id", PropertyCollection)
  | extend SrcIpAddr = iif(SrcIpAddr startswith '::ffff:', replace('::ffff:', '', SrcIpAddr), SrcIpAddr)
  | extend AppId = tolower(replace('/providers/Microsoft.PowerApps/apps/', '', AppId))


